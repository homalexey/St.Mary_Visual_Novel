init:
    default anger_beskrovny = 2 #тут начальный стат злости
    default letter_lifeova = 2 #тут начальный стат для прощального письма

screen hud():
    text "Злость Бескровного: [anger_beskrovny]" xalign 0.97 yalign 0.03 color "#ac1e1e"

# Определение персонажей игры.
image Aleevo = At('Aleev', sprite_highlight('Aleev'))
image Lifeovao = At('Lifeova', sprite_highlight('Lifeova'))
image Beskrovnyo = At('Beskrovny', sprite_highlight('Beskrovny'))
image Timofeevo = At('Timofeev', sprite_highlight('Timofeev'))
image Matveevo = At('Matveev', sprite_highlight('Matveev'))

define a = Character(_('Алеев'), callback = name_callback, cb_name = "Aleev", color="#cd8c00")
define z = Character(_('Жизневская'), color="#3ba459", callback = name_callback, cb_name = "Lifeova")
define b = Character(_('Бескровный'), color="#ac1e1e", callback = name_callback, cb_name = "Beskrovny")
define t = Character(_('Тимофеев'), color="#6f4747", callback = name_callback, cb_name = "Timofeev")
define m = Character(_('Матвеев'), color="#6d320d", callback = name_callback, cb_name = "Matveev")

define narrator = Character(callback = name_callback, cb_name = None)

#персонажики
image Beskrovny: 
    "Beskrovny normal.png"
    zoom 0.52

image Lifeova:
    "Lifeova normal.png"
    zoom 0.65

image Aleev:
    "Aleev normal.png"
    zoom 0.56

image Timofeev:
    "Timofeev normal.png"
    zoom 0.56

image Matveev:
    "Matveev normal.png"
    zoom 0.58

#фоны под размер экрана
image bg outside us = Transform("bg outside us.jpg", fit="contain")
image bg cap room us = Transform("bg cap room us.jpg", fit="contain")
image bg exodus us = Transform("bg exodus us.jpg", fit="contain")
image bg jackson us = Transform("bg jackson us.jpg", fit="contain")
image bg kitchen one us = Transform("bg kitchen one us.jpg", fit="contain")
image bg medic room one us = Transform("bg medic room one us.jpg", fit="contain")
image bg semen one us = Transform("bg semen one us.jpg", fit="contain")

# Игра начинается здесь:
label start:

    scene bg outside us
    play music '01. nosferatu.mp3'

    "Один меткий выстрел — все, что нужно команде для свежего медвежьего мяса."
    "Темные глубины глаз грузного зверя проглатывают веру стрелка в успех."
    "Он целится, неосознанно отступая назад, к кораблю, по хрустящему снегу."
    "Руки трясутся от холода и усталости."
    "Безоружные матросы во главе со штурманом Алеевым спрятались за сугробами и наблюдали за исходом их самоубийственной охоты."
    "Остался один патрон."
    "..."

    "Небо плотно затянуто серой пеленой."
    "Редкий снег порошит дрейфующий остров."
    "Силуэт застрявшего во льдах корабля чудится абсурдным сном."
    "Там, за обледенелыми досками, под рваными парусами исхудавший экипаж ждет хороших новостей."
    "..."

    play sound 'sound/misfire.mp3'
    "Ружье клинит."
    play sound 'sound/bear.mp3'
    "Медведь, ни секунды не сомневаясь своим звериным рассудком, бросается на матроса."
    play sound 'sound/rip flesh.mp3'
    "Тихо, словно снежное облако, он проскользил до своей жертвы, мощным ударом лапы выбил оружие из рук и одним укусом растерзал бедолаге шею."
    "..."

    "Штурман Алеев не стал молча наблюдать."
    play sound 'sound/scream anger.mp3'
    "Он вылез из своего укрытия и с громким матерным криком побежал на медведя."
    "За ним рванули остальные охотники."
    play sound 'sound/bear.mp3'
    "Напуганный медведь успел оторвать голову матроса — с ней он устремился прочь в свое логово."
    
    show Aleevo at left
    show Timofeevo at right
    with dissolve

    t "Что будем делать?"
    a "Сегодня без мяса. Тело на борт. Похороним завтра."

    "Они откопали припрятанные сани и положили на них мертвого друга. Алеев подобрал ружье."
    
    t "Это уже третья неудачная охота. Нас все меньше. И силы на исходе."

    menu:
        "Ответить":
            a "Знаю."
        
        "Промолчать":
            a "..."

    hide Aleevo
    hide Timofeevo
    with dissolve

    show bg medic room one us with fade
    play music "Alien.mp3"

    "Чтобы прийти в себя, штурман решил помочь судовому медику Софье Жизневской — молодой и хорошенькой девушке — провести инвентаризацию оставшихся медикаментов."
    "Он сидел за столом и вписывал данные, которые ему диктовала подруга, пока ковырялась в ящиках и перебирала пустые склянки."
    "Она заметно устала."
    "Как и все на этой проклятой шхуне."
    "..."

    "В 1912 году, в августе, «Святая Мария» отплыла из порта в Санкт-Петербурге, чтобы первой под российским флагом пройти Северным морским путем до Владивостока."
    "В сентябре, в Александровске-на-Мурмане, часть экипажа отказалась участвовать в безрассудной экспедиции в самом конце летней навигации, и капитан Бескровный был вынужден набрать матросов с окрестных улиц и кабаков."
    "Тогда же на борт попала и малоопытная Жизневская."

    show Aleevo at left
    show Lifeovao at right
    with dissolve

    z "Ложечная трава закончилась."

    menu:
        "Плохая новость":
            a "И кровь мы не добыли."
            z "Значит, будет цинга."
        
        "Еще одна плохая новость":
            a "А нас все так же тянет на север..."

    "Софья упала на стул перед штурманом, закрыла глаза."
    "Веки дергались."
    "Она машинально разминала пальцы."
    "Алеев в журнале выводил “0” напротив графы с ложечницей."

    z "Когда это кончится…"

    "За нее говорила невообразимая изнуренность."
    "Алеев думал о том, что не даст окружающей тьме переварить ее в меланхолию."
    "..."

    hide Aleevo
    hide Lifeovao
    with dissolve

    play sound 'sound/step.mp3'
    "В каюту без стука зашел Тимофеев. Худое бледное лицо, желтые глаза навыкате."

    show Timofeevo
    with dissolve
    
    t "Ужин готов. Сегодня будет мясо."

    "Глаза матроса заблестели от последней фразы."
    "Штурман смотрел на него с немым вопросом."
    "Тимофеев лишь слабо кивнул и удалился в столовую."
    "..."

    hide Timofeevo
    with dissolve

    show bg kitchen one us with fade

    "За столом теснились восемнадцать членов экипажа."
    "Несколько жировых и газовых ламп раскидывали исхудалые тени по стенам."
    "В кружках остывал чай. На тарелках у каждого — соблазнительный кусок жареного мяса."
    "..."

    "Тимофеев под руку ввел в столовую больного капитана Бескровного."
    "Только недавно он начал приходить в себя после затяжной болезни, природа которой так и осталась невыясненной."
    "Он долго не мог есть, не терпел света солнца и спал, когда все бодрствовали."
    "Но он вернулся, к радости большинства."
    "Даже при том, что сейчас он больше напоминал облитый белым воском скелет."

    show Beskrovnyo
    with dissolve

    b "Перед началом трапезы предлагаю помянуть нашего товарища, отдавшего жизнь в попытке спасти от голода наши собственные."
    b "Его отвага не будет забыта."
    b "Его вклад в освоение севера тем более."
    b "Я никого не виню в его смерти."
    b "Но пять человек не уберегли одного?"
    b "Конечно, всякое случается."

    show screen hud with dissolve
    
    menu:
        "Защищаться (Злость +1)":
            $ anger_beskrovny += 1
            show Aleevo at left with dissolve
            show Beskrovnyo at right with move
            a "Легко говорить, сутками не выходя на солнце."
            b "Ты не справился, Алеев. Точка."
            hide Aleevo with dissolve
            show Beskrovnyo at center with move

        "Не усугублять":
                "Штурман предпочел не реагировать на укол капитана."
                "Ему казалось, что тот просто ревнует, ведь Алееву пришлось руководить шхуной, пока тот лежал в постели с недугом."
                "Перепалка команде ни к чему."

    hide screen hud with dissolve

    b "Когда одни пытались обуздать белого медведя, другие выловили тюленя, чье мясо мы с вами имеем возможность вкусить, и чью кровь выпить. Она в графине."
    b "Приятного аппетита!"

    hide Beskrovnyo
    with dissolve

    "Весь прием пищи Бескровный наблюдал, как его экипаж почти не жуя поедает сочное мясо."
    "Тимофеев держал его стакан с кровью всегда полным — это единственное, что капитан употреблял в этот вечер."
    "Плоть и кровь тюленя в две минуты забурлили в животах полярников."
    "Это был самый желанный и самый вкусный ужин последних полутора лет, с тех самых пор, как шхуна покинула Империю."
    "..."

    show bg semen one us
    with fade
    play music '02. room.mp3'

    "Во сне, ворочаясь в своем спальнике и теплой одежде среди остальных товарищей, штурман Алеев видел кошмар."
    "В их общую с матросами каюту, что стала таковой после разбора межкомнатных перегородок на дрова, проникла тень."
    "Ее пальцы скользили по лицам полярников."
    "Ее призрачная поступь не отдавалась скрипом половиц."
    play sound 'sound/sniff.mp3'
    "Она наклонилась над штурманом и вожделенно вдохнула воздух возле его лица."

    show screen hud with dissolve
    
    if anger_beskrovny == 3:
        "Что-то острое коснулось шеи Алеева."
        "Пыталось вонзиться в плоть, высвободить холодеющую кровь."
        "Но отступило."
        "Тень пропала."
        "Сон сменился черной тишиной."
    
    if anger_beskrovny == 2:
        play sound 'sound/sheets.mp3'
        "Кто-то громко перевернулся."
        "Тень пропала."
        "Сон сменился черной тишиной."

    hide screen hud with dissolve

    scene bg outside us with fade
    play music '01. nosferatu.mp3'

    "Повар Матвеев в одиночку волок по снегу ящик с припрятанным мясом."
    "Запасов осталось мало, хватит на один хороший ужин, но тем не менее ослабевший двухметровый полярник едва мог бы сам поднять груз на борт."

    show Aleevo at left
    show Matveevo at right
    with dissolve

    a "Помогу."

    "Пока они вдвоем толкали холодильник к шхуне, повар не спускал с него глаз."
    "Они обвязали груз тросом, чтобы поднять на палубу."

    m "Я сам. Спасибо."
    a "Не будет лишним, если останусь."

    "Уже через пару минут на палубе они пытались ухватить и поставить ящик так, чтобы из него не вывалилось содержимое."
    "Но веревка соскользнула, а замерзшие руки не удержали резко навалившейся тяжести."
    play sound 'sound/box.mp3'
    "Холодильник рухнул на бок."
    "Крышка с хлопком ударилась о пол."
    "Из-за кровавой наледи высунулась бледная рука погибшего матроса."
    "..."

    "Матвеев со страхом смотрел на Алеева."
    "Алеев не отрывал глаз от замороженных останков бывшего товарища."

    m "Я м-могу объяснить…"
    a "Тюлень?"

    "Матвеев кивнул."

    a "Капитан?"

    "Матвеев снова кивнул."

    a "Закрой ящик. И убери подальше."

    hide Aleevo
    hide Matveevo
    with dissolve

    show bg cap room us with fade
    play music '04 - Temporary salvation.mp3'

    "В каюте Бескровного было темно."
    "Лишь одна небольшая лампа теплым светом отгоняла тень с его осунувшегося белого лица."
    "Тимофеев за столом пытался под осколками этого света вести записи в бортовом журнале."
    play sound 'sound/step.mp3'
    "Алеев вошел громко и без стука."

    show Aleevo
    with dissolve

    a "Я видел тюленя."

    show Aleevo at left with move
    show Beskrovnyo at right
    with dissolve
    
    b "Прекрасное животное."
    a "Наш вчерашний ужин."

    "Тимофеев отложил карандаш."
    "Бескровный только улыбнулся."
    "За закрытыми веками не было движения глаз."

    show screen hud with dissolve

    b "Цинга."
    b "Цинга нас скоро погубит."
    b "Или же то, что почти погубило меня."
    b "Нам нужно мясо."
    b "Нужна кровь."
    a "Не человеческая."
    b "Любая."
    b "Смотри, какой талант оказался Матвеев, никто не отличил от настоящего тюленя!"

    menu:
        "Отвращение":
            a "Безумие!"
        "Факты":
            a "За полтора года дрейфа мы потеряли четверых."
            b "Но съели только одного."
    
    b "Мы слабеем."
    b "Земля все дальше."
    b "Вокруг — лед и медведи."
    b "Охота все чаще заканчивается неудачно."
    b "Если мы хотим дождаться, пока течение вынесет нас к Земле Франца-Иосифа или Шпицбергену, нам нужно научиться принимать решения."

    #здесь злобы может быть от 2 до 4
    menu:
        "Прямой вопрос (Злость +1)":
            $ anger_beskrovny += 1
            a "И кормить экипаж человечиной?"
            b "Ты начинаешь меня нервировать."
            b "Я здесь капитан, а не ты."
            b "Я несу ответственность за шхуну и каждого, кто на ней обитает."
            b "Я принимаю решения."
            "Капитан предложил сделку: Алеев сейчас же выйдет на охоту с Тимофеевым и добудет животное."
        
        "Принятие":
            "Штурман молча согласился."
            "Как и согласился на сделку: он выйдет на охоту с Тимофеевым и добудет животное."
            
    "Тогда останки матроса будут похоронены, а не употреблены."
    "И экипаж никогда не узнает о случившемся."

    hide screen hud
    hide Aleevo
    hide Beskrovnyo
    with dissolve

    show bg outside us with fade
    play music '01. nosferatu.mp3'

    show Aleevo at left
    show Timofeevo at right
    with dissolve

    "Они вышли с одним ружьем, которое нес Тимофеев."
    "Алееву достались топор и пара ножей со сколотыми лезвиями."
    "Они отдалялись от «Святой Марии», пока снег совсем не спрятал ее от их взоров."
    "Медведей не было."
    "..."

    "Тимофеев шел за Алеевым."
    "Он направил ствол штурману в затылок."

    play sound 'sound/misfire.mp3'
    "Осечка."

    "Механизм сработал громко, но безрезультатно."
    "Штурман повернулся к своему несостоявшемуся убийце как раз в тот момент, когда на лицо матроса бесшумно опустилась когтистая лапа."
    play sound 'sound/bear.mp3'
    "Алеев упал на спину, отполз подальше и достал топор."
    play sound 'sound/chew.mp3'
    "Мощные челюсти разрывали глотку Тимофеева, разбрызгивая кровь по снегу и морде зверя."
    "Лапы рвали одежду, пробираясь к грудной клетке."
    "Но медведю быстро надоело ковыряться в слоях шерсти и перьев."
    "Он повернулся к Алееву."
    "..."

    hide Timofeevo with pixellate
    show Aleevo at center with move  

    menu:
        "Сгруппироваться":
            "Штурман медленно свернулся калачиком, прижав голову к коленям и закрыв ее руками."
            "Стал тише дышать."
            "Прислушиваться."
            "Скоро медведь ушел."
            "Но штурман успел промерзнуть до костей."
        
        "Встать":
            "Штурман медленно встал на ноги, готовый отбиваться от атаки."
            "Он лишний раз не шевелился, не дал повода хищнику принять его за угрозу или добычу."
            "Медведь изучал его, но не приближался."
            "Алеев стал медленно обходить зверя."
            "Ботинком он нащупал засыпанное снегом ружье и осторожно взял в руки."
            "Поднял над собой и чуть наклонил в сторону медведя."
            "Тот стал отступать."
            play sound 'sound/bear.mp3'
            "Штурман сделал два шага вперед — животное попятилось, а потом и вовсе с ревом убежало."
            "..."

    "Алеев похоронил разодранный труп Тимофеева в неглубокой яме в снегу."

    hide Aleevo
    with dissolve

    "На этот раз на шхуну мертвец не вернется."

    show bg kitchen one us with fade
    play music '04 - Temporary salvation.mp3'

    "К ужину Алеев так и не появился на «Святой Марии»."

    show Beskrovnyo
    with dissolve

    b "Вторая потеря за два дня."
    b "И снова в охотничьей команде штурмана Алеева."
    b "Еще рано делать заключения о смерти охотников, но не будем отрицать очевидное."
    b "Помянем."

    "Вверх взметнулись чашки, наполненные кровью."
    "Жизневская печально смотрела на капитана."
    "Опустошенные сосуды одновременно и с грохотом обрушились на деревянный стол."

    show Lifeovao at left with dissolve
    show Beskrovnyo at right with move

    z "Может, отправить команду?"
    b "Нет."
    b "Уже поздно."

    hide Beskrovnyo
    hide Lifeovao
    with dissolve

    show bg medic room one us with fade
    
    "Перед отходом ко сну капитан зашел в каюту к Софье."
    "Слабо горела лампа."
    "Голый, он активно двигал бедрами, навалившись на Жизневскую сверху."
    "Она тяжело дышала и чуть постанывала."
    "Глаза капитана светились, как у кошки."
    "Их костлявые тела завязывались в паукообразные фигуры, они в поту скользили друг на друге, пока холод лизал их обнаженную плоть."
    "..."

    "Алеев стоял за дверью в каюту, наблюдая за ними через тонкую щелочку."
    "Ему казалось, что по стенам будто растянулась многорукая тень с длинными тонкими пальцами из недавнего кошмара."

    scene bg semen one us
    with fade
    play music 'kms4 (inverted house).mp3'

    "К утру пропал еще один матрос."
    "Все видели, как он ложился спать."
    "Никто не заметил, как он ушел."
    "На кухне Матвеев старательно разделывал мясо только что пойманного тюленя."
    "..."

    show bg cap room us with fade

    play sound 'sound/step.mp3'
    "Бескровный спал мертвым сном, когда Алеев явился в его каюту."
    "Он обвязал ниткой нательного крестика кулак, и свесил серебряное изображение Христа над капитаном."

    "Бледное существо пробудилось и, от увиденного, с ужасом вжалось в постель."

    show Beskrovnyo
    show screen hud 
    with dissolve

    b "Ты мертв!"

    show Aleevo at left
    show Beskrovnyo at right with move
    with dissolve

    #тут уже стата 2-5
    menu:
        "Сразу к делу":
            a "А ты упырь."
            a "Я оставил записку Софье. Она расскажет остальным."
            a "Найдет на палубе открытый ящик с потрохами матросов..."
            a "Либо мы заключим сделку."
        
        "Съязвить (Злость +1)":
            $ anger_beskrovny += 1
            a "Мертвых мы едим, а не смотрим на них с ужасом."
            a "Но тобой даже собаку стыдно кормить."
            a "Упырь."
            "Белое лицо Бескровного стало почти прозрачным. По нему прошла рябь гнева."
            a "Предлагаю сделку."
            a "Я оставил записку Софье. Она расскажет все остальным."
            a "Найдет на палубе открытый ящик с потрохами матросов..."
            a "Либо..."

    a "Я храню твою тайну, а смерти экипажа прекращаются."

    "В глазах Бескровного не было жизни, Алеев словно смотрел в лицо трупа."

    b "Почему мне не убить тебя при случае?"
    a "Попробуй."
    b "Мне нужна кровь."
    a "Все еще есть тюлени."

    play sound 'sound/hiss.mp3'
    "Шипение и свист вырвались из пересушенной глотки капитана."
    "Он вынужден был согласиться на условия."
    "Алеев занес в каюту подготовленный графин с животной кровью."
    "Уже через десять секунд сосуд был пуст, а сделка заключена."
    "..."
    
    hide screen hud
    hide Beskrovnyo
    hide Aleevo
    with dissolve

    show bg outside us with fade

    "На шхуне не осталось никаких чувств, чтобы выразить радость от возвращения штурмана после целой ночи в снежной пустыне."
    "Все, что он получил — невесомые хлопки по плечу от едва передвигающихся матросов."
    "..."

    "Матвеев похоронил людей вместе с уже приготовленными кусками их мяса."
    "Дневная охота оказалась успешной, и запасы еды на «Святой Марии» пополнились свежими тушами медвежат."
    "В глазах экипажа снова начала разгораться надежда."
    "..."

    show bg medic room one us with fade
    play music 'Alien.mp3'

    "Вечером, в каюте Жизневской, штурман вновь помогал ей с журналами."
    "Он старался не смотреть на ее нежное лицо, которое не способны были испортить ни впалые щеки, ни чрезмерная сухость пухлых губ."
    "Но на этом лице все еще отражался экстаз прошлой ночи."

    show Aleevo
    with dissolve

    a "Нам нужно на юг."

    show Lifeovao at right
    show Aleevo at left with move
    with dissolve

    z "Дрейф тащит на север."
    a "Значит, нужно идти пешком."

    "Софья села рядом с ним и отобрала карандаш."
    "Он все-таки взглянул на нее."
    "В ее глазах мерцала жизнь — и только у нее одной из всего экипажа."
    "Алеев был готов утонуть в ее зеленом море, умереть прямо сейчас и больше не чувствовать ничего."

    menu:
        "Спросить о важном":
            $ letter_lifeova += 1
            a "Ты пойдешь со мной?"
            z "Я не знаю."
            z "Капитан, и все остальные…"

        "Промолчать":
            a "..."

    hide Aleevo
    hide Lifeovao
    with dissolve

    "Когда они впервые встретились, был светлый сентябрьский день 1912 года в Александровске — самом северном городе Империи."
    "Алеев молча глядел с палубы на Екатерининский остров и старался не замечать, как за спиной дюжина матросов с вещами покидает шхуну."
    "Это были опытные моряки, и они ясно понимали, что идти северными водами в самом конце летней навигации равно смерти."
    "Лед обязательно пожрет корабль еще в Баренцевом море."
    "Алеев разделял их страх."
    "Но сам покинуть шхуну не решался: хотя бы один опытный офицер должен остаться с Бескровным в его легкомысленном приключении."
    "..."

    "Капитан окликнул его. Штурман нехотя обернулся."
    "Бескровный, маневрируя между матросами и их чемоданами, пробирался к нему с возбужденным видом."
    "Он нашел людей в экипаж."
    "Да, большинство из них были максимум рыбаки, а в основном вообще пьянчуги, но их уверенность в миссии и в капитане не оставила в нем сомнений в их надежности."
    "А самое главное — с ними в плавание отправится новый судовой медик, сестра милосердия и дальняя родственница Бескровного."
    "На этих словах Алеев заметил выглядывающее из-за спины капитана пламя зеленых глаз юной девушки."
    "Она совсем недавно приплыла с матерью и сестрами из Норвегии и сразу предложила свою помощь на борту «Святой Марии»."
    "Жизневская улыбнулась вместо приветствия, и переживания Алеева по поводу экспедиции растворились в прохладном воздухе Александровска."
    "..."

    show bg outside us with fade

    "К середине весны 1914 года он уже два месяца тайно сговаривался с экипажем об исходе со шхуны, строил каяки и сани из досок «Святой Марии»."
    "Он не хотел умирать в ледовом плену."
    "Не хотел мириться с отвергнутостью."
    "Не хотел более делить кров с упырем, которого вряд ли возможно убить."
    "..."

    "В конце концов, этот заговор открылся Бескровному."

    show bg kitchen one us with fade
    play music 'kms4 (inverted house).mp3'

    show Beskrovnyo
    show screen hud 
    with dissolve

    b "Я не разрешаю!"
    b "Дрейф вполне однозначно ведет нас к земле."
    b "А в этом походе вы все умрете уже через два дня!"

    show Aleevo at left
    show Beskrovnyo at right with move
    with dissolve

    menu:
        "Успокоить":
            a "Кто не хочет, тот не идет."

        "Огрызнуться (Злость +1)":
            $ anger_beskrovny += 1
            a "Все смерти только на совести капитана."
    
    b "Штурман привел к гибели не одного человека, и вы все равно хотите доверить ему свои жизни?!"

    play sound 'sound/plate.mp3'
    "Тощая рука Бескровного смела со стола тарелку с едой."
    "Он вперил взгляд в Алеева."
    "Штурман чувствовал, что спор может зайти слишком далеко."

    #тут уже стата до 7 может разогнаться
    menu:
        "Высказаться (Злость +1)":
            $ anger_beskrovny += 1
            a "Мы не хотим продолжать проливать кровь ради больного и эгоистичного капитана, потерявшего здравый смысл."
            b "А придется."
        
        "Успокоить (Злость -1)":
            $ anger_beskrovny -= 1
            a "Два года назад ты доверился мне. Помнишь?"

    hide screen hud
    hide Beskrovnyo
    with dissolve
    
    play sound 'sound/table hit.mp3'
    "Бескровный, стукнул кулаком по столу и удалился к себе."
    "..."

    hide Aleevo
    with dissolve

    if anger_beskrovny >= 5:
        jump fair_death
    else:
        jump go_far

label fair_death:

    scene bg outside us with fade
    play music '02 - Lights off.mp3'

    show Aleevo
    with dissolve

    show screen hud with dissolve

    "Штурман проснулся от дикого холода."
    "Плотная темнота застилала обзор."
    "На ощупь Алеев пытался определить хотя бы каюту, но каждый раз пальцы натыкались на ледяную корку, а не на древесину."
    "..."

    play sound 'sound/bear.mp3'
    "Раздался рев медведя."
    "..."

    "Тень, что обволакивала мужчину, отступила."
    "Показалось звездное небо. Снежные дюны. Полная луна."

    show Aleevo at left with move
    show Beskrovny at right with dissolve
    "Бескровный стоял напротив, скаля зубы в нездоровой улыбке."
    "Шхуны на горизонте не было."

    a "Капитан?"

    "Ответа не было."
    "Штурман сделал шаг навстречу."
    "Капитан улыбнулся шире."
    "Еще шаг, и со рта Бескровного по бороде и на землю потекли струйки густой слюны."
    "..."

    "Внезапно он согнулся в припадке."
    "Упал на снег, стал трястись."
    "Алеев не решался подойти ближе."
    "И правильно."
    "Бескровный стал выгибаться, стонать и кряхтеть. Его одежда расходилась по швам. Волосы на теле моментально потеряли цвет, стали длиннее и гуще."
    play sound 'sound/loud bear.mp3'
    "Морда вытянулась и заорала по-медвежьи."
    "Всего через минуту перед штурманом оказался не его старый приятель, но большой разгневанный медведь."
    "Алеев не мог двинуться с места от шока."
    "..."

    play sound 'sound/hit flesh.mp3'
    "Бескровный-медведь одним рывком добрался до жертвы и содрал когтистой лапой лицо штурмана."
    play sound 'sound/rip flesh.mp3'
    "Следующий удар вспорол живот, вывалив горячие внутренности на северный воздух."
    play sound 'sound/chew.mp3'
    "Затем капитан перегрыз Алееву глотку и впился губами в кровоточащую рану."
    "..."

    hide Aleevo with pixellate
    show Beskrovny at center with move
    hide screen hud with dissolve

    "«Святая Мария» осталась обречена на тяжелую смерть во льдах."

    jump credits

    return

label go_far:

    scene bg semen one us with fade
    play music '02 - Lights off.mp3'

    "Ночью вернулась длиннопалая тень."
    "Сначала она просто плыла по каюте экипажа, гладила лица невесомой ладонью."
    "Бесшумно ступала на промерзшие доски, ветром скользя под звуки трения льда о перекрытия."

    play sound 'sound/bonebreak.mp3'
    "А потом раздался треск."
    play sound 'sound/wheeze.mp3'
    "И хрип."
    play sound 'sound/rip flesh.mp3'
    "Люди стали кричать, но голоса по очереди смолкали с тяжелыми влажными ударами о стены и полы."
    "Кровь теплыми брызгами орошала помещение, наполняя воздух железным привкусом."
    "В едва просачивающемся свете луны штурман увидел Бескровного, впившегося зубами в шею матроса."
    "Кто-то бежал в атаку на него и тут же был убит, а кто-то выбегал наружу."
    "Алеев вынул крест и вытянул руку с ним вперед."
    play sound 'sound/hiss.mp3'
    "Упырь, почувствовав это, стал медленно отступать по липкому от крови полу, пока снова не обратился в тень и не исчез в своей каюте."
    "..."

    "Выжившие зажгли свет."
    "Из шестнадцати спящих в живых осталось пятеро."
    "Всю ночь они вытаскивали трупы на мороз, чтобы утром похоронить."

    show Aleevo
    with dissolve

    a "Мы должны уйти сегодня, иначе завтра будет некому."

    hide Aleevo
    with dissolve

    show bg medic room one us with fade

    "Алеев зашел в каюту Софьи, но та спала чудовищно крепким сном и будто совсем не делала вдохов."
    "На ее шее и подушке в свете лампы переливались точечные следы крови."
    "Алеев едва ее разбудил."

    show Aleevo
    with dissolve

    a "Нам нужно уходить."

    show Lifeovao at right with dissolve
    show Aleevo at left with move

    z "Капитан пойдет?"

    menu:
        "Правда":
            $ letter_lifeova += 1
            a "Нет."
            z "Тогда я остаюсь."
        
        "Уйти от ответа":
            a "Теперь я здесь капитан."
            z "..."

    hide Lifeovao
    with dissolve

    "Она снова отключилась."

    scene bg exodus us with fade
    play music '04 - Temporary salvation.mp3'

    "Команда из пяти человек отправилась на юг, пока Софья и Бескровный спали своим дневным сном."
    "На нартах они везли провизию, полевую кухню, письма, личные дневники и два самодельных каяка."
    "Маршрут был построен по неточной, почти фантастической карте Фритьофа Нансена из старой книги."
    "Они могли определять свое местоположение лишь с одним секстантом — других навигационных инструментов у экипажа не осталось."
    "..."

    "Снег стоял как туман."
    "Солнечные очки, собранные из стеклянных бутылок, едва помогали не ослепнуть в белой пустыне."
    "Дикие животные кричали где-то неподалеку."
    "Алеев пытался сдерживать команду от постоянных порывов сдаться, вернуться на шхуну или вовсе лечь и погибнуть под насыпью снега."
    "Один матрос сбежал, прихватив единственные часы, рабочую винтовку и бинокль — так и пропал."
    "..."

    play sound 'sound/campfire.mp3'
    "Ночью четверо грелись у огня."
    "Матвеев отвечал за еду: сухари и тюлений жир с чаем."
    "Мерцали звезды."

    show Aleevo
    with dissolve

    a "Нас тянет на север."
    a "Сколько бы не шли на юг, мы дрейфуем на север."

    hide Aleevo
    with dissolve

    "В палатке, прижавшись друг к другу, полярники пытались уснуть при свете ламп, но никому не удалось увидеть глубокий восстанавливающий сон."
    "Смерть заглянула каждому в голову, проникла в их души и ледяным дыханием затушила каждый слабый огонек надежды в их сердцах."

    show Matveevo
    with dissolve

    m "Лучше бы остался на борту."

    "Прошептал Матвеев за секунду до того, как снаружи стало так тихо, что звук собственного сердцебиения стал оглушительным."
    "..."

    hide Matveevo with dissolve

    play music '02 - Lights off.mp3'

    "Длиннопалая тень ощупывала ткань палатки, пытаясь обнаружить вход."
    "Алеев, так и не сомкнувший глаза, вскинул приготовленную винтовку..."
    "...тихо произнес молитву..."
    play sound 'sound/gunshot.mp3'
    "...и пуля с грохотом прошла сквозь монстра."
    "Моряки выскочили из палатки, готовые атаковать врага ножами, топорами и всем подручным."
    play sound 'sound/bear.mp3'
    "Вокруг лагеря сгрудилась дюжина белых медведей."
    "Бескровный стоял между ними, с аппетитом обсасывая рану на оторванной голове беглого матроса."
    "В его белой шубе чернела дыра от выстрела."
    "..."

    play sound 'sound/loud bear.mp3'
    "Он выкинул свой перекус в снег, и медведи, восприняв это за сигнал, с диким топотом набросились на людей."
    play sound 'sound/rip flesh.mp3'
    "Всего какие-то секунды — и двое матросов оказались растерзаны."
    "Матвеев с Алеевым убежали за ближайшие сугробы."
    play sound 'sound/reload gun.mp3'
    "Штурман перезаряжал ружье."
    play sound 'sound/hiss.mp3'
    "Бескровный настиг Матвеева, подвернувшего ногу, и вцепился ему в шею."
    "Когда Алеев повернулся для выстрела, их уже не было."
    "Только запах железа растекался по воздуху."
    "..."

    "Он вернулся к лагерю."
    "Угли костра слабо перемигивались."
    "Среди крови и кишок товарищей, все еще испускающих жаркий пар, штурман не нашел выживших."
    "Он стал отгонять оставшихся медведей криком и размахиванием винтовкой, и те послушно отступили в темноту ночи."
    "У порога палатки он заметил вползающую внутрь паукообразную тень."

    menu:
        "Прицелиться":
            "Подошел ближе, вскинув винтовку."

        "Попятиться":
            "Отошел подальше, не уводя взгляд с шевелений."

    play sound 'sound/bear.mp3'
    "Белый медведь выпрыгнул из темноты, в воздухе принимая форму капитана."
    play sound 'sound/gunshot.mp3'
    "Пуля пронзила упыря, и тот отлетел в сторону, как перо от порыва ветра."
    "Под снегом, куда упал Бескровный, оказалась глубокая расщелина, и он едва успел зацепиться за край одной рукой."
    "Сил выкарабкаться у него не хватало."

    show Beskrovnyo at right
    show Aleevo at left
    with dissolve

    b "Помоги мне!"
    b "Я не буду тебя убивать!"

    menu:
        "Поверить":
            "Алеев отвел дуло ружья в сторону, присел на корточки и протянул руку."
            "Капитан одним резким взмахом свободной руки попытался вцепиться в ладонь штурмана, пронзить когтями и потянуть на себя."
            "Но Алеев вовремя увернулся."
            "Встал и направил оружие в голову врага."
        
        "Не верить":
            play sound 'sound/reload gun.mp3'
            "Алеев перезарядил ружье и направил в голову капитану."
            "Тот зашипел, обнажив ряд острых зубов и вытянув необыкновенно длинный язык."

    play sound 'sound/misfire.mp3'
    "Осечка."
    "Бескровный засмеялся, чуть соскальзывая в пропасть."
    play sound 'sound/punch.ogg'
    "Алеев перевернул ружье и прикладом отбил упырю пальцы."
    play sound 'sound/hiss.mp3'
    "Бледное существо упало на дно ямы, откуда не переставало выплевывать оскорбления и проклятья."
    "..."

    hide Beskrovnyo with dissolve
    show Aleevo at center with move

    "Без шансов в одиночку закончить поход на юг, штурману оставалось лишь вернуться на «Святую Марию»."
    "С собой он взял только винтовку."
    "..."

    hide Aleevo
    with dissolve

    show bg outside us with fade
    play music '04 - Temporary salvation.mp3'

    "К середине дня Алеев снова был на шхуне."
    "Мертвая тишина."
    "Ветер разносил снег по палубе и каютам."
    play sound 'sound/woodcrack.flac'
    "Лед, сковавший «Святую Марию», сдавливал судно с таким громким треском, какого раньше не было."
    "Кровь застыла на стенах корабля."
    "Это последний день экспедиции Бескровного — и последний день шхуны."
    "..."

    menu:
        "Зайти к Софье":
            show bg medic room one us with fade
            $ letter_lifeova += 1
            "Штурман прошел в каюту Жизневской, где та все еще лежала в постели."
            "Он пощупал ее пульс, прислонил ухо к губам."
            "Она мертва."
            "В ее широко открытых остекленевших глазах навсегда пропал огонь."
            "Грудь больше не вздымалась от вздохов."
            "Алеев лег рядом."
            "Огонь пропал и в нем."
            "Он умрет рядом с ней."
            "..."
        
        "Зайти в капитанскую":
            show bg cap room us with fade
            "Штурман прошел в каюту Бескровного."
            "Капитан не оставил дневников. Судовой журнал был наполовину сожжен."
            "Алеев прилег в кровать врага, игнорируя гнилостный смрад его белья."
            "В нем не осталось сил, чтобы жить."
            "Хоть бы были еще силы, чтобы умереть."

    show bg outside us with fade
    
    play sound 'sound/woodcrack.flac'
    "Доски трещали от натиска льдов."
    play sound 'sound/box.mp3'
    "Ветер задувал сквозь вновь образовавшиеся щели."
    "Алеев закрыл глаза."
    "Шхуна стала валиться на бок."
    "Долгожданное умиротворение с тяжестью навалилось на последнего члена экипажа «Святой Марии»."
    "..."

    "Ему стал сниться сон."
    "Яркий и красочный, от него веяло теплом из прошлой жизни."
    "Он видел себя, как он подростком пускается по реке на свою первую экспедицию на самодельной лодке."
    "Как поступает в Санкт-Петербургское училище дальнего плавания."
    "Как он ходил по Енисею."
    "Как встретил Жизневскую."
    "Алеев чувствовал обволакивающий аромат ее кожи."
    "Его окутало тепло ее рук."
    "Шум кричащей в агонии палубы стал оглушительным."
    "Софья улыбнулась во сне, и ее улыбка сделала сон невыносимо светлым."
    "..."

    scene bg jackson us with fade
    play music 'Alien.mp3'

    "Штурман проснулся от слепящего солнца."
    "Он с трудом пришел в себя, оказавшись в какой-то деревянной будке на берегу плещущегося моря."
    "Софьи рядом не было."
    "Но была еда, спички и жестяная банка с двумя посланиями."
    "Алеев развернул первое письмо."

    if letter_lifeova >= 4:
        show Lifeovao
        with dissolve
        z "Прощай."
        z "Мне жаль, что я не смогла стать голосом разума."
        z "Жаль, что не смогла повлиять на настроения команды."
        z "Что моя вера не уберегла нас."
        z "Наверное, молитвы на севере не достигают ушей Бога, иначе как объяснить эту дьявольскую пляску в наших душах."
        z "Капитан научил меня молиться по-другому… другим."
        z "Я поддалась его влиянию, будто одурманенная, наверное даже влюбилась в эти таинства… и в него."
        z "А теперь я проклята."
        z "Но я спасла тебя… хотя бы тебя."
        z "Ты научил меня управляться с картами — и этого хватило, чтобы я сообразила дорогу до мыса Флоры, до Базы Джексона."
        z "Ты вернешься в Россию."
        z "Мне дороги туда нет."
        z "Прости за все, твоя Софья Жизневская."
    else:
        show Lifeovao
        with dissolve
        z "Прощай. Твоя Софья Жизневская."

    hide Lifeovao
    with dissolve

    "Слезы Алеева покатились по заросшему бородой лицу."
    "Он вдохнул запах письма, надеясь вновь уловить аромат ее кожи;"
    "приложился губами, чтобы почувствовать ее прощальный поцелуй."
    play sound 'sound/matchburn.mp3'
    "В отчаянии и гневе смял листок и сжег его спичкой."
    "..."

    "Во втором письме было предупреждение, что через два дня здесь в сторону Санкт-Петербурга проплывет поисковое судно «Мученик Георгий»."
    "Оно отправлено для спасения экипажа «Святой Марии»."

    jump credits

    return

label credits:
    scene black
    with fade

    show text "{size=40}Спасибо за игру!\n\nРассказ: Алексей Хомячук\nАрт: Алексей Хомячук x Шедеврум x ChatGPT x GigaChat\nМузыка: Алексей Хомячук\nКод: Алексей Хомячук x ChatGPT\n\n2025{/size}" at scroll_credits
    pause 15
    return

transform scroll_credits:
    yalign 1.5      # старт ниже экрана
    linear 15.0 yalign -0.5  # за 15 сек. уходит вверх



